#include "Encoder.h"

#define NOT_AN_INTERRUPT 50

uint8_t buttonPin;
uint8_t encoderInterrupt;
byte bMask;

boolean encoderIsOn = false;
boolean buttonPressed = false;
boolean buttonLongPressed = false;
boolean killPress = false;
boolean buttonIsOn = false;

volatile byte *bReg;
volatile char encoderCounter;

void Encoder_initEncoder(uint8_t encA_int_pin, uint8_t encB_pin) 
{
	pinMode(encA_int_pin, INPUT_PULLUP);
	pinMode(encB_pin, INPUT_PULLUP);

	bMask = digitalPinToBitMask(encB_pin);
	bReg = portInputRegister(digitalPinToPort(encB_pin));
	encoderInterrupt = digitalPinToInterrupt(encA_int_pin);
}

void Encoder_initButton(uint8_t button_pin)
{
	buttonPin = button_pin;
	pinMode(buttonPin, INPUT_PULLUP);
}

void Encoder_Init(uint8_t button_pin, uint8_t encA_int_pin, uint8_t encB_pin)
{
	Encoder_initEncoder(encA_int_pin, encB_pin);
	Encoder_initButton(button_pin);
}

void ISR_encoder_handler() {
	//  My encoder has a full cycle of 4 transitions per click.
	//  I only want one increment or decrement per click
	//  So we are only looking at one transition
	//  We're only using the falling edge on the interrupt pin.
	//  So we know ENCODER_INTERRUPT_PIN is LOW.
	//  If ENCODER_B_PIN is HIGH then they're different so decrement
	//  if(digitalRead(ENCODER_B_PIN) == HIGH) {
	if (*bReg & bMask) {
		encoderCounter--;
	} else {
		encoderCounter++;
	}
}

void Encoder_EncoderOn() {
	attachInterrupt(encoderInterrupt, ISR_encoder_handler, FALLING);
	encoderCounter = 0;
	encoderIsOn = true;
}

void Encoder_EncoderOff() {
	detachInterrupt(encoderInterrupt);
	encoderCounter = 0;
	encoderIsOn = false;
}

char Encoder_CheckEncoder() {
	if (encoderIsOn) {
		cli();
		char retval = encoderCounter;
		encoderCounter = 0;
		sei();
		return retval;
	} else {
		return 0;
	}
}

/*
const char asciiSet[] PROGMEM = {'a','b','c','d','e','f','g','h','i',
		'j','k','l','m','n','o','p','q','r','s','t','u','v','w','x','y','z',
		'0','1','2','3','4','5','6','7','8','9','A','B','C','D','E','F','G',
		'H','I','J','K','L','M','N','O','P','Q','R','S','T','U','V','W','X',
		'Y','Z','.','\?','/','\\','*','-','_','+'     // INSERT MORE HERE, KEEP SPACE LAST
		, ' '};

const int sizeOfAsciiSet = sizeof(asciiSet) / sizeof(asciiSet[0]);



int findCharIndexInAsciiSet(char aVal) {
	for (int i = 0; i < sizeOfAsciiSet; i++){
		char chr = pgm_read_byte_near(asciiSet + i);
		if(chr == aVal){
			return i;
		}
	}
	return 0;  // return 0 by default.
}

void useRotaryEncoderASCII(char& var) {
	int cre = checkRotaryEncoder();
	int chr = findCharIndexInAsciiSet(var);

	chr += cre;
	chr %= sizeOfAsciiSet;
	while(chr < 0){   // fix it if it went negative.  Use while just in case it went way negative.
		chr += sizeOfAsciiSet;
	}

	var = pgm_read_byte_near(asciiSet + chr);
}
*/

boolean Encoder_PeekEncoder()
{
	return encoderCounter != 0;
}

boolean Encoder_IsEncoderOn()
{
	return encoderIsOn;
}

void Encoder_PollButton()
{
	static boolean buttonState = HIGH;
	static boolean lastState = HIGH;
	static unsigned long pressTime = 0;
	if (buttonIsOn) 
	{
		boolean curState = digitalRead(buttonPin);
		if (curState != lastState) 
		{
			pressTime = millis();
		}
		if ((millis() - pressTime) > BUTTON_DEBOUNCE) //button has been in present state long enough
		{  
			if ((buttonState == LOW) && (curState == HIGH)) // button was pressed and is being released
			{
				if (!killPress) //Prevents registering a press on the release from a long press
				{
					buttonPressed = true;    // register a button press
				}
				killPress = false;
			}
			// buttonState is the fully debounced state of the button for the rest of the program to use
			buttonState = curState;
		}

		if ((buttonState == LOW) && (millis() - pressTime > BUTTON_LONG_PRESS)) 
		{
			buttonLongPressed = true;
			killPress = true;    //  Force to skip next press when button is released after long press
		}
		lastState = curState;
	}
}

void Encoder_clearButton()
{
	buttonPressed = false;
	buttonLongPressed = false;
}

boolean Encoder_CheckButtonLongPress()
{
	boolean retval = buttonLongPressed;
	buttonLongPressed = false;
	return retval;
}

boolean Encoder_CheckButton()
{
	boolean retval = buttonPressed;
	buttonPressed = false;
	return retval;
}

void Encoder_ButtonOn()
{
	buttonIsOn = true;
	Encoder_clearButton();
}

void Encoder_ButtonOff()
{
	buttonIsOn = false;
	Encoder_clearButton();
}

boolean Encoder_IsButtonOn()
{
	return buttonIsOn;
}